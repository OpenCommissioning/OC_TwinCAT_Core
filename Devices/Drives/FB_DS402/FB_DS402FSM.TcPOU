<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_DS402FSM" Id="{6c215d9f-5c7d-0c39-10a1-e57f60fb32d8}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DS402FSM
VAR_INPUT
	bModeSpecific 				: BOOL := TRUE;
	bError 						: BOOL;			
	stControl					: ST_DS402_6040h_ControlWord;
END_VAR

VAR_OUTPUT	
	stStatus					: ST_DS402_6041h_StatusWord;	
END_VAR

VAR	
	_eState 					: E_DS402State := E_DS402State.S0_NotReadyToSwitchOn;	
	rTrig_QuickStop 			: R_TRIG;
	rTrig_Reset 				: R_TRIG;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[stStatus.b12_OperationModeSpecific := bModeSpecific;

rTrig_QuickStop(CLK := stControl.b2_QuickStop);
rTrig_Reset(CLK := stControl.b7_FaultReset);

CASE _eState OF
	E_DS402State.S0_NotReadyToSwitchOn : S0_NotReadyToSwitchOn();
	E_DS402State.S1_SwitchOnDisabled : S1_SwitchOnDisabled();
	E_DS402State.S2_ReadyToSwitchOn : S2_ReadyToSwitchOn();
	E_DS402State.S3_SwitchedOn : S3_SwitchedOn();
	E_DS402State.S4_OperationEnabled : S4_OperationEnabled();
	E_DS402State.S5_Fault : S5_Fault();	
	E_DS402State.S6_FaultReactionActive : S6_FaultReactionActive();	
	E_DS402State.S7_QuickStopActive : S7_QuickStopActive();			
END_CASE]]></ST>
    </Implementation>
    <Property Name="eState" Id="{c0c11fdc-e209-0bc7-268c-837f3122d28a}">
      <Declaration><![CDATA[PROPERTY eState : E_DS402State]]></Declaration>
      <Get Name="Get" Id="{c902b08d-33fd-030b-02e1-9082b32caede}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[eState := _eState;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{f49a806b-af5e-0919-2dc8-a5dab5d683b0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_eState := eState;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="IsDisableOperation" Id="{2077861b-dd63-044f-0ff4-d573daec2beb}">
      <Declaration><![CDATA[METHOD PRIVATE IsDisableOperation : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsDisableOperation := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop 
AND NOT stControl.b3_EnableOperation
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsDisableVoltage" Id="{c4f2bd25-ae27-0920-27e5-e47120442aff}">
      <Declaration><![CDATA[METHOD PRIVATE IsDisableVoltage : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsDisableVoltage := 
NOT stControl.b1_EnableVoltage
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsEnableOperation" Id="{78963b22-d521-0fcc-1451-1ae88f5788c9}">
      <Declaration><![CDATA[METHOD PRIVATE IsEnableOperation : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsEnableOperation := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop 
AND stControl.b3_EnableOperation
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsFaultReset" Id="{78dec10b-bfe8-0c12-085c-416177c55974}">
      <Declaration><![CDATA[METHOD PRIVATE IsFaultReset : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsFaultReset := rTrig_Reset.Q;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsQuickStop" Id="{c7fe78f8-5d8a-005b-0fe2-8952006734a5}">
      <Declaration><![CDATA[METHOD PRIVATE IsQuickStop : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsQuickStop := 
NOT stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsShutdown" Id="{589a4e2f-ab40-06e8-0be4-c0f6375beb39}">
      <Declaration><![CDATA[METHOD PRIVATE IsShutdown : BOOL


]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsShutdown := 
NOT stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsSwitchOn" Id="{857d6295-cca9-05f2-38d4-4f72367fd3d8}">
      <Declaration><![CDATA[METHOD PRIVATE IsSwitchOn : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsSwitchOn := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Property Name="nControl" Id="{be5b2660-71e1-0f4a-0922-baae27640e83}">
      <Declaration><![CDATA[PROPERTY nControl : WORD]]></Declaration>
      <Get Name="Get" Id="{44f57f63-a1af-095d-0719-352f028617e0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nControl.0	:= stControl.b0_SwitchOn;					         
nControl.1	:= stControl.b1_EnableVoltage;				      	
nControl.2	:= stControl.b2_QuickStop;					         	
nControl.3	:= stControl.b3_EnableOperation;			     	
nControl.4	:= stControl.b4_OperationModeSpecific;			
nControl.5	:= stControl.b5_OperationModeSpecific;			
nControl.6	:= stControl.b6_OperationModeSpecific;			
nControl.7	:= stControl.b7_FaultReset;					        	
nControl.8	:= stControl.b8_Reserved;					          	
nControl.9	:= stControl.b9_Reserved;					          
nControl.10	:= stControl.b10_Reserved;					         	
nControl.11	:= stControl.b11_Reserved;					         	
nControl.12	:= stControl.b12_Reserved;					         	
nControl.13	:= stControl.b13_Reserved;					         	
nControl.14	:= stControl.b14_Reserved;					         	
nControl.15 := stControl.b15_Reserved;					         ]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{327e6121-681f-0573-1ce5-728d8ed56878}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[stControl.b0_SwitchOn 					:= 	nControl.0;	
stControl.b1_EnableVoltage				:= 	nControl.1;		
stControl.b2_QuickStop					:= 	nControl.2;		
stControl.b3_EnableOperation			:= 	nControl.3;		
stControl.b4_OperationModeSpecific		:= 	nControl.4;		
stControl.b5_OperationModeSpecific		:= 	nControl.5;		
stControl.b6_OperationModeSpecific		:= 	nControl.6;		
stControl.b7_FaultReset					:= 	nControl.7;		
stControl.b8_Reserved					:= 	nControl.8;		
stControl.b9_Reserved					:= 	nControl.9;		
stControl.b10_Reserved					:= 	nControl.10;		
stControl.b11_Reserved					:= 	nControl.11;		
stControl.b12_Reserved					:= 	nControl.12;		
stControl.b13_Reserved					:= 	nControl.13;		
stControl.b14_Reserved					:= 	nControl.14;		
stControl.b15_Reserved					:= 	nControl.15;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="nStatus" Id="{ad2ac296-dce6-0f57-07ed-2b794d7c1670}">
      <Declaration><![CDATA[PROPERTY nStatus : WORD]]></Declaration>
      <Get Name="Get" Id="{3bd0117e-c9c6-0229-21ef-1fcce3db90ad}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nStatus.0 := stStatus.b0_ReadyToSwitchOn; 			
nStatus.1 := stStatus.b1_SwitchedOn; 				
nStatus.2 := stStatus.b2_OperationEnabled;			
nStatus.3 := stStatus.b3_Fault;					
nStatus.4 := stStatus.b4_VoltageEnabled;
nStatus.5 := stStatus.b5_QuickStop;
nStatus.6 := stStatus.b6_SwitchOnDisabled;			
nStatus.7 := stStatus.b7_Warning;					
nStatus.8 := stStatus.b8_Synchranisation;			
nStatus.9 := stStatus.b9_Remote;			
nStatus.10 := stStatus.b10_TargetReached;				
nStatus.11 := stStatus.b11_InternalLimitActive;		
nStatus.12 := stStatus.b12_OperationModeSpecific;			
nStatus.13 := stStatus.b13_OperationModeSpecific;			
nStatus.14 := stStatus.b14_OperationModeSpecific;			
nStatus.15 := stStatus.b15_ClosedLoopActive;	]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="S0_NotReadyToSwitchOn" Id="{68ef7ed5-62ad-0f2b-0a95-3a1f2538be9c}">
      <Declaration><![CDATA[METHOD PRIVATE S0_NotReadyToSwitchOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn 				:= FALSE;
stStatus.b2_OperationEnabled		:= FALSE;
stStatus.b3_Fault					:= FALSE;
stStatus.b4_VoltageEnabled			:= FALSE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

_eState := E_DS402State.S1_SwitchOnDisabled;]]></ST>
      </Implementation>
    </Method>
    <Method Name="S1_SwitchOnDisabled" Id="{6ca582d6-313b-0d89-1d9b-ed66bcec48de}">
      <Declaration><![CDATA[METHOD PRIVATE S1_SwitchOnDisabled : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b4_VoltageEnabled			:= FALSE;
stStatus.b6_SwitchOnDisabled 		:= TRUE;

IF IsShutdown() THEN
	_eState := E_DS402State.S2_ReadyToSwitchOn;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S2_ReadyToSwitchOn" Id="{592d6928-069b-0e0a-0e69-c1379d91af33}">
      <Declaration><![CDATA[METHOD PRIVATE S2_ReadyToSwitchOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b4_VoltageEnabled			:= TRUE;
stStatus.b5_QuickStop 				:= TRUE; 
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF bError THEN
	_eState := E_DS402State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() OR IsQuickStop() THEN
	_eState := E_DS402State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsSwitchOn() THEN
	_eState := E_DS402State.S3_SwitchedOn;
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="S3_SwitchedOn" Id="{7f680533-2da8-0175-1a8b-638f719e598b}">
      <Declaration><![CDATA[METHOD PRIVATE S3_SwitchedOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b5_QuickStop 				:= TRUE; 
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF bError THEN
	_eState := E_DS402State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() OR IsQuickStop() THEN
	_eState := E_DS402State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsShutdown() THEN
	_eState := E_DS402State.S2_ReadyToSwitchOn;
	RETURN;
END_IF

IF IsEnableOperation() THEN
	_eState := E_DS402State.S4_OperationEnabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S4_OperationEnabled" Id="{0d0f32b7-75cc-06a9-3f50-e7cb286f0e60}">
      <Declaration><![CDATA[METHOD PRIVATE S4_OperationEnabled : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn				:= TRUE;
stStatus.b1_SwitchedOn 					:= TRUE;
stStatus.b2_OperationEnabled 			:= TRUE;
stStatus.b3_Fault 						:= FALSE;
stStatus.b5_QuickStop 					:= TRUE; 
stStatus.b6_SwitchOnDisabled 			:= FALSE;

IF bError THEN
	_eState := E_DS402State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() THEN
	_eState := E_DS402State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsShutdown() THEN
	_eState := E_DS402State.S2_ReadyToSwitchOn;
	RETURN;
END_IF

IF IsQuickStop() THEN
	_eState := E_DS402State.S7_QuickStopActive;
	RETURN;
END_IF

IF IsDisableOperation() THEN
	_eState := E_DS402State.S3_SwitchedOn;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S5_Fault" Id="{d0189520-8efb-0804-3f13-3ca18e9c9110}">
      <Declaration><![CDATA[METHOD PRIVATE S5_Fault : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= TRUE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF IsFaultReset() THEN
	_eState := E_DS402State.S1_SwitchOnDisabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S6_FaultReactionActive" Id="{c330e5b3-45c6-09a3-3061-b31ab23ea460}">
      <Declaration><![CDATA[METHOD PRIVATE S6_FaultReactionActive : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= TRUE;
stStatus.b3_Fault 					:= TRUE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

_eState := E_DS402State.S5_Fault;]]></ST>
      </Implementation>
    </Method>
    <Method Name="S7_QuickStopActive" Id="{fd5c76e1-bfeb-04a0-0a11-fe08d6b6cab5}">
      <Declaration><![CDATA[METHOD PRIVATE S7_QuickStopActive : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= TRUE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b5_QuickStop 				:= FALSE; 
stStatus.b6_SwitchOnDisabled		:= FALSE;

IF IsDisableVoltage() THEN
	_eState := E_DS402State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsEnableOperation() THEN
	_eState := E_DS402State.S4_OperationEnabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_DS402FSM">
      <LineId Id="114" Count="1" />
      <LineId Id="80" Count="0" />
      <LineId Id="122" Count="0" />
      <LineId Id="56" Count="1" />
      <LineId Id="63" Count="0" />
      <LineId Id="67" Count="6" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.eState.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.eState.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsDisableOperation">
      <LineId Id="22" Count="4" />
      <LineId Id="10" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsDisableVoltage">
      <LineId Id="16" Count="1" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsEnableOperation">
      <LineId Id="23" Count="4" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsFaultReset">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsQuickStop">
      <LineId Id="16" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="11" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsShutdown">
      <LineId Id="40" Count="3" />
      <LineId Id="25" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.IsSwitchOn">
      <LineId Id="14" Count="3" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.nControl.Get">
      <LineId Id="5" Count="14" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.nControl.Set">
      <LineId Id="5" Count="14" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.nStatus.Get">
      <LineId Id="5" Count="14" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S0_NotReadyToSwitchOn">
      <LineId Id="5" Count="2" />
      <LineId Id="12" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="10" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S1_SwitchOnDisabled">
      <LineId Id="26" Count="8" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S2_ReadyToSwitchOn">
      <LineId Id="17" Count="3" />
      <LineId Id="22" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="38" Count="2" />
      <LineId Id="37" Count="0" />
      <LineId Id="14" Count="2" />
      <LineId Id="23" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="27" Count="4" />
      <LineId Id="26" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S3_SwitchedOn">
      <LineId Id="9" Count="3" />
      <LineId Id="14" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="34" Count="2" />
      <LineId Id="38" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="16" Count="3" />
      <LineId Id="15" Count="0" />
      <LineId Id="22" Count="3" />
      <LineId Id="20" Count="0" />
      <LineId Id="27" Count="3" />
      <LineId Id="26" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S4_OperationEnabled">
      <LineId Id="9" Count="3" />
      <LineId Id="14" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="40" Count="2" />
      <LineId Id="39" Count="0" />
      <LineId Id="16" Count="9" />
      <LineId Id="31" Count="3" />
      <LineId Id="30" Count="0" />
      <LineId Id="26" Count="3" />
      <LineId Id="15" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S5_Fault">
      <LineId Id="9" Count="3" />
      <LineId Id="5" Count="0" />
      <LineId Id="16" Count="3" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S6_FaultReactionActive">
      <LineId Id="6" Count="3" />
      <LineId Id="5" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="12" Count="0" />
    </LineIds>
    <LineIds Name="FB_DS402FSM.S7_QuickStopActive">
      <LineId Id="6" Count="3" />
      <LineId Id="11" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="13" Count="3" />
      <LineId Id="12" Count="0" />
      <LineId Id="18" Count="3" />
      <LineId Id="17" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>